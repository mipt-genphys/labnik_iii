import elabnik.BuildHTMLTask
import elabnik.BuildPdfTask
import elabnik.ProcessSvgTask

buildscript {
    project.ext.outputDir = "build/pdf"
}

plugins {
    id 'base'
}

task preamplePdf(type: BuildPdfTask) {
    group = "latex"
    description "Build preamble as pdf"
    source = "Preamble"
}

task buildPdf(type: BuildPdfTask) {
    group = "latex"
    description "Build whole labnik as pdf"
    source = "Labnik-III"
}

// Creating build tasks for individual chapters
(1..6).collect { num ->
    tasks.create("chapter${num}Pdf", BuildPdfTask){
        group = "latex"
        description "Build Chapter $num as pdf"
        target = "Chapter_$num"
        parameters = [
                title: "Chapter_$num",
                fragment: "Chapter_${num}",
                chapter: num
        ]
        source = "Template"
    }

    tasks.create("chapter${num}Html", BuildHTMLTask){
        group = "html"
        description "Build Chapter $num as html"
        target = "Chapter_$num"
        parameters = [
                title: "Chapter_$num",
                fragment: "Chapter_${num}",
                chapter: num
        ]
        source = "Template"
    }
}

task chaptersPdf{
    group = "latex"
    description = "Build all chapters as pdfs"
    (1..6).collect{
        dependsOn(":chapter${it}Pdf")
    }
}

task testPdf(type: BuildPdfTask) {
    group = "latex"
    description "pdf test"
    source = "Test"
}

task processSvg(type: ProcessSvgTask){
    group = "html"
    description "Process and copy svg resources"
    sourceDir = file("Images")
}
